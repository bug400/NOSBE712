XTRACT,T0,NT2.
COMMENT. THIS EXTRACTS PP PROGS AND LIBRARIES FROM THE TUB DS TAPE
COMMENT. FOR MNF, PL/1, ALGOL60 AND ALGOL68
COMMENT. REQUIRES TAPE NBE_DS_L664.TAP
COMMENT. EXTRACTS CCI MODULES 0D0, 0D1, 0DF AND 0DZ FROM TAPE CCI.TAP
ATTACH,USERNUC,ID=CCT.
LIBRARY,USERNUC.
VSN(TAPE=TUB000)
REQUEST(TAPE,NT)
REQUEST,TNUC,*PF.
REQUEST,TOVL,*PF.
REQUEST,TA68L,*PF.
REQUEST,TAL5L,*PF.
REQUEST,TPL1L,*PF.
REQUEST,TMNFL,*PF.
COMMENT.
COMMENT. EXTRACT UMP PP ROUTINE REQUIRED FOR MNF PMD
COMMENT.
COPYBF,TAPE,TEMP.
REWIND,TEMP.
SKIPF,TEMP,144.
COPYBR,TEMP,UMP.
REWIND,UMP.
REWIND,TAPE.
COMMENT.
COMMENT. COPY LIBRARIES FROM DS TAPE
COMMENT.
EDITLIB(SYSTEM,ERROR=6)
RETURN,TAPE.
VSN(TAPE=CCI000)
REQUEST(TAPE,NT)
COMMENT.
COMMENT. EXTRACT CCI MODULES
COMMENT.
REWIND,TAPE.
COPYBR,TAPE,T0D0.
REWIND,T0D0.
COPYBR,TAPE,T0D1.
REWIND,T0D1.
SKIPF,TAPE,13.
COPYBR,TAPE,T0DF.
REWIND,T0DF.
COPYBR,TAPE,T0DZ.
REWIND,T0DZ.
COMMENT.
COMMENT. PUT UMP AND CCI MODULES INTO USERPP
COMMENT.
ATTACH,USERPP,ID=CCT,PW=XYZ,RW=1.
EDITLIB(SYSTEM,MSGL=6,ERROR=3)
EXTEND(USERPP)
RETURN,USERPP.
CATALOG,TNUC,ID=CCT.
CATALOG,TOVL,ID=CCT.
CATALOG,TA68L,ID=CCT.
CATALOG,TAL5L,ID=CCT.
CATALOG,TPL1L,ID=CCT.
CATALOG,TMNFL,ID=CCT.
COMMENT. *** XTRACT COMPLETE ***
EXIT(S)
COMMENT. *** XTRACT FAILED ***
~eor
LIBRARY(TNUC,NEW)
SKIPF(1,TAPE,F)
ADD(*,TAPE)
FINISH.
LIBRARY(TOVL,NEW)
ADD(*,TAPE)
FINISH.
LIBRARY(TAL5L,NEW)
SKIPF(13,TAPE,F)
ADD(*,TAPE)
FINISH.
LIBRARY(TPL1L,NEW)
SKIPF(1,TAPE,F)
ADD(*,TAPE)
FINISH.
LIBRARY(TA68L,NEW)
ADD(*,TAPE)
FINISH.
LIBRARY(TMNFL,NEW)
SKIPF(1,TAPE,F)
ADD(*,TAPE)
FINISH.
ENDRUN.
~eor
LIBRARY(USERPP,OLD)
REPLACE(UMP,UMP)
REPLACE(0D0,T0D0)
REPLACE(0D1,T0D1)
REPLACE(0DF,T0DF)
REPLACE(0DZ,T0DZ)
FINISH.
ENDRUN.
~eor
~eoi
